(*
    This file is a part of IsarMathLib - 
    a library of formalized mathematics for Isabelle/Isar.

    Copyright (C) 2025  Slawomir Kolodynski

    This program is free software; Redistribution and use in source and binary forms, 
    with or without modification, are permitted provided that the following conditions are met:

   1. Redistributions of source code must retain the above copyright notice, 
   this list of conditions and the following disclaimer.
   2. Redistributions in binary form must reproduce the above copyright notice, 
   this list of conditions and the following disclaimer in the documentation and/or 
   other materials provided with the distribution.
   3. The name of the author may not be used to endorse or promote products 
   derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR IMPLIED 
WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF 
MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. 
IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, 
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; 
OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, 
WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR 
OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, 
EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

*)

section\<open>Uniformity defined by a a collection of pseudometrics\<close>

theory MetricUniform_ZF imports FinOrd_ZF_1 MetricSpace_ZF UniformityLattice_ZF

begin

text\<open>In the \<open>MetricSpace_ZF\<close> we show how a single (ordered loop valued) pseudometric
  defines a uniformity. In this theory we extend this to the situation where we have
  an arbitrary collection of pseudometrics, all defined on the the same set $X$ and valued
  in an ordered loop $L$. Since real numbers form an ordered loop all results proven in
  this theory are true for the standard real-valued pseudometrics. \<close>

subsection\<open>Uniformity defined by a collection of pseudometrics\<close>

text\<open>Suppose $\mathcal{M}$ is a collection of (an ordered loop valued) pseudometrics on $X$,
  i.e. $d:X\times X\rightarrow L^+$ is a pseudometric for every $d\in \mathcal{M}$.
  Then, for each $d\in \mathcal{M}$ the collection of sets 
  $\mathfrak{B} = \{ d^{-1}(\{c\in L^+: c\leq b\}): b \in L_+ \}$
  forms a fundamental system of entourages. Consequently, by taking supersets of 
  $\mathfrak{B}$ we can get a uniformity defined by the pseudometric 
  (see theorem \<open>metric_gauge_base\<close> in the \<open>MetricSpace_ZF\<close> theory. 
  Repeating this process for each $d\in \mathcal{M}$ we obtain a collection of uniformities
  on $X$. Since all uniformities on $X$ ordered by inclusion form a complete lattice
  (see theorem \<open>uniformities_compl_latt\<close> in the \<open>UniformityLattice_ZF\<close>) there exists the smallest 
  (coarsest) uniformity that contains all uniformities defined by the 
  pseudometrics in $\mathcal{M}$. \<close>

text\<open>To shorten the main definition let's define the uniformity derived from a single
  pseudometric as \<open>UnifFromPseudometric\<close>. In the definition below $X$ is the underlying set,
  $L,A,r$ are the carrier, binary operation and the order relation, resp., of the ordered
  loop where the pseudometrics takes its values and $d$ is the pseudometrics.\<close>

definition
  "UnifFromPseudometric(X,L,A,r,d) \<equiv> Supersets(X\<times>X,UniformGauge(X,L,A,r,d))"

text\<open>We construct the uniformity from a collection of pseudometrics $\mathcal{M}$ by taking the
  supremumum (i.e. the least upper upper bound) of the collection of
  uniformities defined by each pseudometric from $\mathcal{M}$. \<close>

definition 
  "UnifFromPseudometrics(X,L,A,r,\<M>) \<equiv> LUB_Unif(X,{UnifFromPseudometric(X,L,A,r,d). d\<in>\<M>})"

text\<open>The context \<open>muliple_pmetric\<close> is very similar to the \<open>pmetric_space\<close> context
  except that rather than fixing a single pseudometric $d$ we fix a 
  nonempty collection of pseudometrics $\mathcal{M}$. That forces the notation for
  \<open>disk\<close>, topology, interior and closure to depend on the pseudometric $d$.
  The $\mathcal{U}$ symbol will denote the collection of uniformities
  generated by $\mathcal{M}$. We also assume that the positive cone $L_+$ 
  of the loop is nonempty, $r$ down-directs $L_+$ (see \<open>Order_ZF\<close> for definition)
  and that the (positive cone of the) ordered loop is halfable (see \<open>MetricSpace_ZF)\<close>). 
  In short, we assume what we need for ordered loop valued pseudometrics to generate uniformities.\<close>

locale muliple_pmetric = loop1 +
  fixes \<M> and X
  assumes nemptyX: "X\<noteq>\<emptyset>"
  assumes nemptyLp: "L\<^sub>+\<noteq>\<emptyset>"
  assumes downdir: "r {down-directs} L\<^sub>+"
  assumes halfable: "IsHalfable(L,A,r)"
  assumes nemptyM: "\<M>\<noteq>\<emptyset>"
  assumes mpmetricAssm: "\<forall>d\<in>\<M>. IsApseudoMetric(d,X,L,A,r)"
  fixes unifs ("\<U>")
  defines unifs_def [simp]: "\<U> \<equiv> {UnifFromPseudometric(X,L,A,r,d). d\<in>\<M>}"
  fixes disk
  defines disk_def [simp]: "disk(d,c,R) \<equiv> Disk(X,d,r,c,R)"
  fixes pmettop ("\<tau>") 
  defines pmettop [simp]: "\<tau>(d) \<equiv> MetricTopology(X,L,A,r,d)"
  fixes interior ("int")
  defines interior_def [simp]: "int(d,D) \<equiv> Interior(D,\<tau>(d))"
  fixes cl
  defines cl_def [simp]: "cl(d,D) \<equiv> Closure(D,\<tau>(d))"

text\<open>If $d$ is one of the pseudometrics from $\mathcal{M}$ then theorems
  proven in the \<open>pmetric_space\<close> context are valid as applied to $d$.\<close>

lemma (in muliple_pmetric) pmetric_space_valid_in_mpm: 
  assumes "d\<in>\<M>" shows "pmetric_space(L,A,r,d,X)" 
proof
  from assms mpmetricAssm show "IsApseudoMetric(d,X,L,A,r)" by simp
qed 

text\<open>In the \<open>muliple_pmetric\<close> context each element of $\mathcal{U}$ is a uniformity.\<close>

lemma (in muliple_pmetric) each_gen_unif_unif: shows "\<U> \<subseteq> Uniformities(X)"
proof
  fix \<Phi> assume "\<Phi>\<in>\<U>"
  then obtain d where "d\<in>\<M>" and "\<Phi> = UnifFromPseudometric(X,L,A,r,d)"
    by auto
  with nemptyX nemptyLp downdir halfable show "\<Phi> \<in> Uniformities(X)"
    using pmetric_space_valid_in_mpm pmetric_space.metric_gauge_base(2) unif_in_unifs
    unfolding UnifFromPseudometric_def by simp
qed

text\<open>The uniformity generated by a family of pseudometrics $\mathcal{M}$ is indeed a uniformity
  which is the supremum of uniformities in $\mathcal{U}$.\<close>

lemma (in muliple_pmetric) gen_unif_unif:
  shows 
    "UnifFromPseudometrics(X,L,A,r,\<M>) {is a uniformity on} X"
    "UnifFromPseudometrics(X,L,A,r,\<M>) = Supremum(OrderOnUniformities(X),\<U>)"
  using nemptyX nemptyM each_gen_unif_unif lub_unif_sup(2,3)
  unfolding UnifFromPseudometrics_def by simp_all

text\<open>The uniformity generated by a family of pseudometrics contains all uniformities
  generated by the pseudometrics in $\mathcal{M}$.\<close>

lemma (in muliple_pmetric) gen_unif_contains_unifs: assumes "\<Phi>\<in>\<U>" 
  shows "\<Phi> \<subseteq> UnifFromPseudometrics(X,L,A,r,\<M>)"
  using nemptyX assms each_gen_unif_unif lub_unif_upper_bound
  unfolding OrderOnUniformities_def InclusionOn_def UnifFromPseudometrics_def
  by simp

text\<open>If a uniformity contains all uniformities generated by the pseudometrics in $\mathcal{M}$
  then it contains the uniformity generated by that family of pseudometrics.\<close>

lemma (in muliple_pmetric) gen_unif_LUB: 
  assumes "\<Psi> {is a uniformity on} X" and "\<forall>\<Phi>\<in>\<U>. \<Phi>\<subseteq>\<Psi>"
  shows "UnifFromPseudometrics(X,L,A,r,\<M>) \<subseteq> \<Psi>"
proof -
  from assms have "\<forall>\<Phi>\<in>\<U>. \<langle>\<Phi>,\<Psi>\<rangle> \<in> OrderOnUniformities(X)"
    using each_gen_unif_unif unif_in_unifs
    unfolding OrderOnUniformities_def InclusionOn_def
    by auto
  with nemptyX nemptyM show ?thesis
    using each_gen_unif_unif lub_unif_lub
    unfolding OrderOnUniformities_def InclusionOn_def UnifFromPseudometrics_def
    by simp
qed

text\<open>The uniformity generated by the collection of pseudometrics $\mathcal{M}$
  contains all inverse images of the initial segments of the positive
  cone of $L$, (i.e. sets of the form $d^{-1}(\{c\in L: 0 \leq c \leq y \})$ for 
  $d\in \mathcal{M}$ and $y$ from the positive cone $L_+$ of $L$).\<close>

lemma (in muliple_pmetric) gen_unif_contains_gauges: assumes "d\<in>\<M>" "y\<in>L\<^sub>+"
  shows "d-``({c\<in>L\<^sup>+. \<langle>c,y\<rangle>\<in>r}) \<in> UnifFromPseudometrics(X,L,A,r,\<M>)"
proof -
  let ?g = "d-``({c\<in>L\<^sup>+. \<langle>c,y\<rangle>\<in>r})"
  from assms(2) have "?g \<in> UniformGauge(X,L,A,r,d)"
    unfolding UniformGauge_def by auto
  with mpmetricAssm assms(1) have "?g \<in> UnifFromPseudometric(X,L,A,r,d)"
    unfolding IsApseudoMetric_def UnifFromPseudometric_def
    using func1_1_L3 superset_gen by auto
  with assms(1) show ?thesis using gen_unif_contains_unifs by auto
qed

text\<open>The uniformity generated by the collection of pseudometrics $\mathcal{M}$
  is the coarsest uniformity that contains all inverse images of the initial 
  segments of the positive cone of $L$ with respect to all $d\in\mathcal{M}$.\<close>

theorem (in muliple_pmetric) gen_unif_corsest: 
  assumes "\<Psi> {is a uniformity on} X" and "\<forall>d\<in>\<M>. \<forall>y\<in>L\<^sub>+. d-``({c\<in>L\<^sup>+. \<langle>c,y\<rangle>\<in>r}) \<in> \<Psi>"
  shows "UnifFromPseudometrics(X,L,A,r,\<M>) \<subseteq> \<Psi>"
proof -
  { fix d assume "d\<in>\<M>"
    with assms(2) have "UniformGauge(X,L,A,r,d) \<subseteq> \<Psi>"
      unfolding UniformGauge_def by auto
    with assms(1) have "UnifFromPseudometric(X,L,A,r,d) \<subseteq> \<Psi>"
      unfolding IsUniformity_def UnifFromPseudometric_def 
      using filter_superset_closed by simp
  } hence "\<forall>\<Phi>\<in>\<U>. \<Phi>\<subseteq>\<Psi>" by simp
  with assms(1) show ?thesis using gen_unif_LUB by simp
qed

subsection\<open>Uniformity defined by a collection of pseudometrics - an alternative approach\<close>

text\<open>This section presents an alternative, more direct approach to defining a uniformity generated
  by a collection of pseudometric. This approach is probably equivalent to the one presented
  in the previous section, but more complicated and hence obsolete.\<close>

text\<open>The next two definitions describe the way a common fundamental system of entourages 
  for $\mathcal{M}$ is constructed. First we take finite subset $M$ of $\mathcal{M}$.
  Then we choose $f:M\rightarrow L_+$. This way for each $d\in M$
  the value $f(d)$ is a positive element of $L$ and 
  $\{d^{-1}(\{c\in L^+: c\leq f(d)\}): d\in M\}$ is a finite collection of 
  subsets of $X\times X$. Then we take intersections of such finite
  collections as $M$ varies over $\mathcal{M}$ and $f$ varies over all possible functions mapping
  $M$ to $L_+$. To simplify notation for this construction we split it into two steps. 
  In the first step we define a collection of finite intersections resulting from 
  choosing a finite set of pseudometrics $M$, $f:M\rightarrow L_+$ and varying 
  the selector function $f$ over the space of functions mapping $M$ to the set of 
  positive elements of $L$. \<close>

definition
  "UniformGaugeSets(X,L,A,r,M) \<equiv> 
  {(\<Inter>d\<in>M. d-``({c\<in>Nonnegative(L,A,r). \<langle>c,f`(d)\<rangle> \<in> r})). f\<in>M\<rightarrow>PositiveSet(L,A,r)}"
  
text\<open>In the second step we collect all uniform gauge sets defined above 
  as parameter $M$ vary over all nonempty finite subsets of $\mathcal{M}$. 
  This is the collection of sets that we will show forms a fundamental system of entourages.\<close>

definition
  "UniformGauges(X,L,A,r,\<M>) \<equiv> \<Union>M\<in>FinPow(\<M>)\<setminus>{\<emptyset>}. UniformGaugeSets(X,L,A,r,M)"

text\<open>Analogously what is done in the \<open>pmetric_space\<close> context 
  we will write \<open>UniformGauges(X,L,A,r,\<M>)\<close> as \<open>\<BB>\<close> in the \<open>muliple_pmetric\<close> context.\<close>

abbreviation (in muliple_pmetric) mgauge ("\<BB>") where "\<BB> \<equiv> UniformGauges(X,L,A,r,\<M>)"

text\<open>The next lemma just shows the definition of $\mathfrak{B}$ in notation 
  used in the \<open>muliple_pmetric\<close>. \<close>

lemma (in muliple_pmetric) mgauge_def_alt: shows 
  "\<BB> = (\<Union>M\<in>FinPow(\<M>)\<setminus>{\<emptyset>}. {(\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})). f\<in>M\<rightarrow>L\<^sub>+})"
  unfolding UniformGaugeSets_def UniformGauges_def by simp

text\<open>$\mathfrak{B}$ consists of (finite) intersections of sets of the
  form $d^{-1}(\{c\in L^+:c\leq f(d)\})$ where $f:M\rightarrow L_+$
  some finite subset $M\subseteq \mathcal{M}$.
  More precisely, if $M$ is a nonempty finite subset of $\mathcal{M}$ and $f$ maps 
  $M$ to the positive set of the loop $L$, then the set 
  $\bigcap_{d\in M} d^{-1}(\{c\in L^+:c\leq f(d)\}$ is in $\mathfrak{B}$.\<close>

lemma (in muliple_pmetric) mgauge_finset_fun:
  assumes "M\<in>FinPow(\<M>)" "M\<noteq>\<emptyset>" "f:M\<rightarrow>L\<^sub>+"
  shows "(\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})) \<in> \<BB>"
  using assms mgauge_def_alt by auto

text\<open>If $d$ is member of any finite subset of $\mathcal{M}$ then 
  $d$ maps $X\times X$ to the nonnegative set of the ordered loop $L$.\<close>

lemma (in muliple_pmetric) each_pmetric_map:
  assumes "M\<in>FinPow(\<M>)" and "d\<in>M" shows "d: X\<times>X \<rightarrow> L\<^sup>+"
    using assms pmetric_space_valid_in_mpm pmetric_space.pmetric_properties(1)
    unfolding FinPow_def by auto

text\<open>Members of the uniform gauge defined by multiple pseudometrics 
  are subsets of $X\times X$ i.e. relations on $X$. \<close>

lemma (in muliple_pmetric) muniform_gauge_relations:
  assumes "B\<in>\<BB>" shows "B\<subseteq>X\<times>X"
proof -
  from assms obtain M f where "M\<in>FinPow(\<M>)" and
    I: "B = (\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)}))"
    using mgauge_def_alt by auto
  { fix d assume "d\<in>M"
    with \<open>M\<in>FinPow(\<M>)\<close> have "d: X\<times>X \<rightarrow> L\<^sup>+"
      using each_pmetric_map by simp
    then have "d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)}) \<subseteq> X\<times>X" using func1_1_L15 by auto
  } with I show ?thesis using inter_subsets_subset by simp
qed

text\<open>Suppose $M_1$ is a subset of $M$ and $\mathcal{M}$.
  $f_1,f$ map $M_1$ and $M$, resp. to $L_+$ and $f\leq f_1$ on $M_1$.
  Then the set $\bigcap_{d\in M} d^{-1}(\{y \in L_+: y\leq f(d)\})$
  is included in the set $\bigcap_{d\in M_1} d^{-1}(\{y \in L_+: y\leq f_1(d)\})$. \<close>

lemma (in muliple_pmetric) fun_inter_vimage_mono:
  assumes "M\<^sub>1\<subseteq>\<M>" "M\<^sub>1\<subseteq>M" "M\<^sub>1\<noteq>\<emptyset>" "f\<^sub>1:M\<^sub>1\<rightarrow>L\<^sub>+" "f:M\<rightarrow>L\<^sub>+" and 
    "\<forall>d\<in>M\<^sub>1. f`(d)\<lsq>f\<^sub>1`(d)"
  shows
     "(\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})) \<subseteq> (\<Inter>d\<in>M\<^sub>1. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)}))"
proof -
  let ?L = "(\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)}))"
  let ?R = "(\<Inter>d\<in>M\<^sub>1. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)}))"
  from assms(2,3) have I: "?L \<subseteq> (\<Inter>d\<in>M\<^sub>1. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)}))"
    using inter_index_mono by simp
  from assms(1,6) have 
    "\<forall>d\<in>M\<^sub>1. (d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})) \<subseteq> d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)})" 
    using pmetric_space_valid_in_mpm pmetric_space.uniform_gauge_mono
      by force
  with assms(2) have  "(\<Inter>d\<in>M\<^sub>1. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})) \<subseteq> ?R" by force
  with I show "?L\<subseteq>?R" by (rule subset_trans)
qed

text\<open>For any two sets $B_1,B_2$ in $\mathfrak{B}$ there exist a third one
  that is contained in both. \<close>

lemma (in muliple_pmetric) mgauge_1st_cond:
  assumes "B\<^sub>1\<in>\<BB>" "B\<^sub>2\<in>\<BB>"
  shows "\<exists>B\<in>\<BB>. B\<subseteq>B\<^sub>1\<inter>B\<^sub>2" 
proof -
  from assms(1,2) obtain M\<^sub>1 f\<^sub>1 M\<^sub>2 f\<^sub>2 where "M\<^sub>1\<noteq>\<emptyset>" "M\<^sub>2\<noteq>\<emptyset>" and
  I: "M\<^sub>1\<in>FinPow(\<M>)" "M\<^sub>2\<in>FinPow(\<M>)" "f\<^sub>1:M\<^sub>1\<rightarrow>L\<^sub>+" "f\<^sub>2:M\<^sub>2\<rightarrow>L\<^sub>+" and  
  II: "B\<^sub>1 = (\<Inter>d\<in>M\<^sub>1. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)}))" "B\<^sub>2 = (\<Inter>d\<in>M\<^sub>2. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>2`(d)}))"
    using mgauge_def_alt by auto
  let ?M\<^sub>3 = "M\<^sub>1\<union>M\<^sub>2"  
  from downdir have "IsDownDirectedSet(L\<^sub>+,r)" 
    using down_directs_directed by simp
  with I have 
    "\<exists>f\<^sub>3\<in>?M\<^sub>3\<rightarrow>L\<^sub>+. (\<forall>d\<in>M\<^sub>1. \<langle>f\<^sub>3`(d),f\<^sub>1`(d)\<rangle>\<in>r) \<and> (\<forall>d\<in>M\<^sub>2. \<langle>f\<^sub>3`(d),f\<^sub>2`(d)\<rangle>\<in>r)"
    using two_fun_low_bound by simp
  then obtain f\<^sub>3 where "f\<^sub>3:?M\<^sub>3\<rightarrow>L\<^sub>+" and
    III: "\<forall>d\<in>M\<^sub>1. f\<^sub>3`(d)\<lsq>f\<^sub>1`(d)" "\<forall>d\<in>M\<^sub>2. f\<^sub>3`(d)\<lsq>f\<^sub>2`(d)"
      by auto
  let ?B\<^sub>3 = "\<Inter>d\<in>?M\<^sub>3. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>3`(d)})"
  from I(1,2) \<open>M\<^sub>1\<noteq>\<emptyset>\<close> \<open>f\<^sub>3:?M\<^sub>3\<rightarrow>L\<^sub>+\<close> have "?B\<^sub>3\<in>\<BB>"
    using union_finpow mgauge_def_alt by auto
  moreover have "?B\<^sub>3\<subseteq>B\<^sub>1\<inter>B\<^sub>2"
  proof - 
    from I(1,2) have "M\<^sub>1\<subseteq>\<M>" "M\<^sub>1\<subseteq>?M\<^sub>3" "M\<^sub>2\<subseteq>\<M>" "M\<^sub>2\<subseteq>?M\<^sub>3"
      unfolding FinPow_def by auto
    with \<open>M\<^sub>1\<noteq>\<emptyset>\<close> \<open>M\<^sub>2\<noteq>\<emptyset>\<close> I(3,4) II \<open>f\<^sub>3:?M\<^sub>3\<rightarrow>L\<^sub>+\<close> III  
    have "?B\<^sub>3\<subseteq>B\<^sub>1" and "?B\<^sub>3\<subseteq>B\<^sub>2" using fun_inter_vimage_mono by simp_all
    thus "?B\<^sub>3\<subseteq>B\<^sub>1\<inter>B\<^sub>2" by auto
  qed
  ultimately show "\<exists>B\<in>\<BB>. B\<subseteq>B\<^sub>1\<inter>B\<^sub>2" by (rule witness_exists)
qed

text\<open>Sets in $\mathfrak{B}$ contain the diagonal and are symmetric,
  hence contain a symmetric subset from $\mathfrak{B}$.\<close>

lemma (in muliple_pmetric) mgauge_2nd_and_3rd_cond: assumes "B\<in>\<BB>" 
  shows "id(X)\<subseteq>B" "B = converse(B)" "\<exists>B\<^sub>2\<in>\<BB>. B\<^sub>2 \<subseteq> converse(B)" 
proof -
  from assms obtain M f where "M\<in>FinPow(\<M>)" "M\<noteq>\<emptyset>" "f:M\<rightarrow>L\<^sub>+" and
    I: "B = (\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)}))"
    using mgauge_def_alt by auto
  { fix d assume "d\<in>M"
    let ?B\<^sub>d = "d-``({c\<in>L\<^sup>+. c\<lsq>f`(d)})"
    from \<open>d\<in>M\<close> \<open>f:M\<rightarrow>L\<^sub>+\<close> \<open>M\<in>FinPow(\<M>)\<close> have
      "pmetric_space(L,A,r,d,X)" and "?B\<^sub>d \<in> UniformGauge(X,L,A,r,d)"
      unfolding FinPow_def UniformGauge_def 
      using apply_funtype pmetric_space_valid_in_mpm by auto
    then have "id(X)\<subseteq>?B\<^sub>d" and "?B\<^sub>d = converse(?B\<^sub>d)" 
      using pmetric_space.gauge_2nd_cond pmetric_space.gauge_symmetric 
      by simp_all
  } with I \<open>M\<noteq>\<emptyset>\<close> show "id(X)\<subseteq>B" and "B = converse(B)" by auto
  from assms \<open>B = converse(B)\<close> show "\<exists>B\<^sub>2\<in>\<BB>. B\<^sub>2 \<subseteq> converse(B)"
    by auto
qed

text\<open>$\mathfrak{B}$ is a subset of the power set of $X\times X$.\<close>

lemma (in muliple_pmetric) mgauge_5thCond: shows "\<BB>\<subseteq>Pow(X\<times>X)"
  using muniform_gauge_relations by auto

text\<open>If $\mathcal{M}$ and $L_+$ are nonempty then $\mathfrak{B}$ is also nonempty.\<close>

lemma (in muliple_pmetric) mgauge_6thCond: shows "\<BB>\<noteq>\<emptyset>" 
proof -
  from nemptyLp nemptyM obtain M y where "M\<in>FinPow(\<M>)" "M\<noteq>\<emptyset>" and "y\<in>L\<^sub>+"
    using finpow_nempty_nempty by blast
  from \<open>y\<in>L\<^sub>+\<close> have "ConstantFunction(M,y):M\<rightarrow>L\<^sub>+" using func1_3_L1 by simp
  with \<open>M\<in>FinPow(\<M>)\<close> \<open>M\<noteq>\<emptyset>\<close> show "\<BB>\<noteq>\<emptyset>" using mgauge_finset_fun by auto
qed

text\<open>If the loop order is halfable then for every set $B_1\in \mathfrak{B}$
  there is another set $B_2\in \mathfrak{B}$ such that $B_2\circ B_2 \subseteq B_1$.\<close>

lemma (in muliple_pmetric) mgauge_4thCond: 
  assumes "B\<^sub>1\<in>\<BB>" shows "\<exists>B\<^sub>2\<in>\<BB>. B\<^sub>2 O B\<^sub>2 \<subseteq> B\<^sub>1"
proof -
  from assms(1) obtain M f\<^sub>1 where "M\<in>FinPow(\<M>)" "M\<noteq>\<emptyset>" "f\<^sub>1:M\<rightarrow>L\<^sub>+" and
    I: "B\<^sub>1 = (\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)}))"
    using mgauge_def_alt by auto
  from halfable \<open>f\<^sub>1:M\<rightarrow>L\<^sub>+\<close> have "\<forall>d\<in>M. \<exists>b\<^sub>2\<in>L\<^sub>+. b\<^sub>2\<ra>b\<^sub>2 \<lsq> f\<^sub>1`(d)"
    using apply_funtype unfolding IsHalfable_def by simp
  with \<open>M\<in>FinPow(\<M>)\<close> have "\<exists>f\<^sub>2\<in>M\<rightarrow>L\<^sub>+. \<forall>d\<in>M. f\<^sub>2`(d) \<ra> f\<^sub>2`(d) \<lsq> f\<^sub>1`(d)"
    unfolding FinPow_def using finite_choice_fun by auto
  then obtain f\<^sub>2 where "f\<^sub>2\<in>M\<rightarrow>L\<^sub>+" and II: "\<forall>d\<in>M. f\<^sub>2`(d) \<ra> f\<^sub>2`(d) \<lsq> f\<^sub>1`(d)"
    by auto
  let ?B\<^sub>2 = "\<Inter>d\<in>M. d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>2`(d)})" 
  { fix d assume "d\<in>M"
    let ?A\<^sub>2 = "d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>2`(d)})"
    from \<open>d\<in>M\<close> \<open>M\<in>FinPow(\<M>)\<close> have "pmetric_space(L,A,r,d,X)"
      unfolding FinPow_def using pmetric_space_valid_in_mpm by auto 
    with \<open>f\<^sub>2\<in>M\<rightarrow>L\<^sub>+\<close> \<open>d\<in>M\<close> II have "?A\<^sub>2 O ?A\<^sub>2 \<subseteq> d-``({c\<in>L\<^sup>+. c\<lsq>f\<^sub>1`(d)})"
      using apply_funtype pmetric_space.half_vimage_square by simp
  } 
  with \<open>M\<noteq>\<emptyset>\<close> I have "?B\<^sub>2 O ?B\<^sub>2 \<subseteq> B\<^sub>1" using square_incl_product by simp
  with \<open>M\<in>FinPow(\<M>)\<close> \<open>M\<noteq>\<emptyset>\<close> \<open>f\<^sub>2\<in>M\<rightarrow>L\<^sub>+\<close> show ?thesis
    using mgauge_finset_fun by auto
qed

text\<open>If $\mathcal{M}$ is a nonempty collection of pseudometrics on a nonempty set $X$
   valued in loop $L$ partially ordered by relation $r$ such that the set of positive elements 
   $L_+$ is nonempty, $r$ down directs $L_+$ and $r$ is halfable on $L$,then
   $\mathfrak{B}$ is a fundamental system of entourages in $X$ hence its supersets 
  form a uniformity on $X$ and hence those supersets define a topology on $X$ \<close>

lemma (in muliple_pmetric) mmetric_gauge_base: 
  shows
    "\<BB> {is a uniform base on} X"
    "Supersets(X\<times>X,\<BB>) {is a uniformity on} X"
    "UniformTopology(Supersets(X\<times>X,\<BB>),X) {is a topology}"
    "\<Union>UniformTopology(Supersets(X\<times>X,\<BB>),X) = X"
  using nemptyX mgauge_1st_cond mgauge_2nd_and_3rd_cond mgauge_4thCond 
    mgauge_5thCond mgauge_6thCond uniformity_base_is_base uniform_top_is_top
  unfolding IsUniformityBaseOn_def by simp_all

subsection\<open>Halving sequences of entourages\<close>

text\<open>The notion of uniformity can be defined in several ways. Our primary definition
  in the \<open>UniformSpace_ZF\<close> theory is based on the concept of entourages. 
  In \<open>UniformSpace_ZF_2\<close> we consider the an alternative definition based on uniform covers.
  The third possible definition is based on families of pseudometrics.
 "The significance of defining a uniformity by means of a family 
  of pseudometrics lies in the fact that all unifomities can be so obtained." 
  (from Bourbaki: General Topology Chapter IX par. 1.4).
  In this section we formalize a part of the material that is needed to prove 
  this statement that can be done without the Axiom of Choice and in the general setting 
  of ordered-loop valued pseudometrics.\<close>

text\<open>The definition of a uniformity requires that for each entourage $U$ 
  there is another one $V$ such that $V\circ V\subseteq U$. Furthermore, 
  lemma \<open>half_size_symm\<close> shows that we can assume $V$ is symmetric.
  In some propositions in this section we will assume that we are given a function say $h$ 
  that provides those half-size entourages, i.e. maps a uniformity $\Phi$ into itself and 
  for each entourage $U\in\Phi$ we have $h(U)\circ h(U)\subseteq V$. 

  The existence of such function follows from the Axiom of Choice, but we don't want to use AC
  in this theory.

  The next definition lists the desired properties of such halving function for shorter
  references in theorem assumptions.\<close>

definition
  IsHalvingFunction ("_ {is a halving function for} _") where
    "h {is a halving function for} \<Phi> \<equiv> 
      h:\<Phi>\<rightarrow>\<Phi> \<and> (\<forall>U\<in>\<Phi>. h`(U) = converse(h`(U)) \<and> h`(U) O h`(U) \<subseteq> U)"

text\<open>Let $\Phi$ be a uniformity on $X$, $U\in\Phi$ and $h$ is a halving function for
  $\Phi$. This function then defines inductively a sequence $\{ H_n\}_{n\in\mathbb{N}}$ of entourages 
  such that $H_0 = U$ and $H_{n+1}\circ H_{n+1}\subseteq H_n$. The next lemma shows
  that $H$ is a $\Phi$-valued sequence which starts at $U$. \<close>

lemma halving_seq_start: 
  assumes "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)"
  shows "H:nat\<rightarrow>\<Phi>" and "H`(0) = U"
  using assms indseq_seq indseq_valat0 unfolding IsHalvingFunction_def by auto

text\<open>For the inductively defined sequence of halving entourages $H$ starting from $U$
   and a natural number $n$ we indeed have $H_{n+1} \circ H_{n+1} \subseteq H_n$. \<close>

lemma halving_seq_halves: assumes "U\<in>\<Phi>" "h {is a halving function for} \<Phi>" "n\<in>nat"
  defines "H \<equiv> InductiveSequence(U,h)"
  shows "H`(n #+ 1) = converse(H`(n #+ 1))" and "H`(n #+ 1) O H`(n #+ 1) \<subseteq> H`(n)"
proof -
  from assms have "H`(n #+ 1) =  h`(H`(n))"
    unfolding IsHalvingFunction_def using indseq_vals succ_add_one(1) by auto
  from assms have "H`(n)\<in>\<Phi>" using halving_seq_start(1) apply_funtype by force
  with assms(2) \<open>H`(n #+ 1) =  h`(H`(n))\<close> show
    "H`(n #+ 1) = converse(H`(n #+ 1))" and "H`(n #+ 1) O H`(n #+ 1) \<subseteq> H`(n)"
    unfolding IsHalvingFunction_def by simp_all
qed

text\<open>A halving sequence in $\Phi$ is decreasing in the inclusion order on $\Phi$,
  hence the inclusion order on $\Phi$ is total on the sequence's image 
  of the natural numbers, hence total on the sequence's image of positive
  natural numbers. We need that fact about positive natural numbers
  because we plan to prove that the image of the sequence on the positive
  natural numbers forms a fundamental system of symmetric entourages.
  and the first element of the sequence (at index $0$) does not have to be symmetric.\<close>

lemma halving_seq_decr: 
  assumes "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)"
  shows 
    "IsDecreasingSeq(\<Phi>,InclusionOn(\<Phi>),H)"
    "InclusionOn(\<Phi>) {is total on} (H``(nat))"
    "InclusionOn(\<Phi>) {is total on} (H``(nat\<setminus>{0}))"
proof -
  from assms(2,3,4) have "H:nat\<rightarrow>\<Phi>" using halving_seq_start(1) by simp
  let ?r = "InclusionOn(\<Phi>)"
  { fix n assume "n\<in>nat"
    with \<open>H:nat\<rightarrow>\<Phi>\<close> have "H`(n) \<in> \<Phi>" and "H`(n #+ 1) \<in> \<Phi>" 
      using apply_funtype by simp_all
    with assms(1) have "H`(n) \<subseteq> X\<times>X" "H`(n #+ 1) \<subseteq> X\<times>X" and "id(X) \<subseteq> H`(n #+ 1)"
      using uni_domain(1) unfolding IsUniformity_def by simp_all
    then have "H`(n #+ 1) \<subseteq> H`(n #+ 1) O H`(n #+ 1)"
      using refl_square_greater by simp
    with assms(2,3,4) \<open>n\<in>nat\<close> \<open>H`(n #+ 1) \<in> \<Phi>\<close> \<open>H`(n) \<in> \<Phi>\<close>
    have "\<langle>H`(n #+ 1),H`(n)\<rangle> \<in> ?r"
      using halving_seq_halves(2) unfolding InclusionOn_def by force
  } hence "\<forall>n\<in>nat. \<langle>H`(n #+ 1),H`(n)\<rangle> \<in> ?r" by simp
  with \<open>H:nat\<rightarrow>\<Phi>\<close> show "IsDecreasingSeq(\<Phi>,InclusionOn(\<Phi>),H)" 
    unfolding IsDecreasingSeq_def by simp
  then show "?r {is total on} H``(nat)"
    using incl_is_partorder decr_seq_total unfolding IsPartOrder_def IsPreorder_def
    by blast
  then show "InclusionOn(\<Phi>) {is total on} (H``(nat\<setminus>{0}))"
    unfolding IsTotal_def by auto
qed

text\<open>We aim at showing that a halving sequence image is a fundamental system of entourages.
  The next lemma shows the first condition in the uniform base definition:
  for two sets in a halving sequence image there is a third one that is contained in both. \<close>

lemma halving_seq_base_1st_cond: 
  assumes "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)" 
  assumes "B\<^sub>1\<in>H``(nat\<setminus>{0})" "B\<^sub>2\<in>H``(nat\<setminus>{0})"
  shows "\<exists>B\<in>H``(nat\<setminus>{0}). B\<subseteq>B\<^sub>1\<inter>B\<^sub>2"
proof -
  let ?r = "InclusionOn(\<Phi>)"
  from assms have "\<langle>B\<^sub>1,B\<^sub>2\<rangle> \<in> ?r \<or> \<langle>B\<^sub>2,B\<^sub>1\<rangle> \<in> ?r"
    using halving_seq_decr(3) unfolding IsTotal_def by simp
  with assms(5,6) show ?thesis unfolding InclusionOn_def 
    by auto
qed

text\<open>Sets in the halving sequence image of positive naturals contain the diagonal, 
  are symmetric and for every such set $B$ there is another one contained in the converse of the
  first one and another one $B_2$ such that $B_2\circ B_2\subseteq B_1$. 
  Note the symmetry of the sets is not required in the definition of a fundamental
  system of entourages, but it's good to have as we plan to construct a pseudometric
  that generates that halving sequence image of positive naturals.\<close>

lemma halving_seq_base_2_3_4_conds: 
  assumes "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)" 
  assumes "B\<in>H``(nat\<setminus>{0})"
  shows 
    "id(X)\<subseteq>B" "B = converse(B)" 
    "\<exists>B\<^sub>1\<in>H``(nat\<setminus>{0}). B\<^sub>1 \<subseteq> converse(B)" 
    "\<exists>B\<^sub>2\<in>H``(nat\<setminus>{0}). B\<^sub>2 O B\<^sub>2 \<subseteq> B"
proof -
  from assms(2,3,4) have "H:nat\<rightarrow>\<Phi>" using halving_seq_start(1) by simp
  with assms(1,5) show "id(X)\<subseteq>B" using entourage_props(2) func1_1_L6(2)
    by blast
  have "nat\<setminus>{0}\<subseteq>nat" by auto
  with assms(5) \<open>H:nat\<rightarrow>\<Phi>\<close> obtain n where "n\<in>nat" "n\<noteq>0" and "B=H`(n)"
    using func_imagedef by auto
  from \<open>n\<in>nat\<close> \<open>n\<noteq>0\<close> obtain k where "k\<in>nat" "n = k #+ 1"
    using nat_not0_succ by auto
  from assms(2,3,4) \<open>k\<in>nat\<close> have "H`(k #+ 1) = converse(H`(k #+ 1))" 
    using halving_seq_halves(1) by simp
  with assms(5) \<open>n = k #+ 1\<close> \<open>B=H`(n)\<close> show 
    "B = converse(B)" and "\<exists>B\<^sub>1\<in>H``(nat\<setminus>{0}). B\<^sub>1 \<subseteq> converse(B)"
    by auto
  from \<open>n\<in>nat\<close> \<open>H:nat\<rightarrow>\<Phi>\<close> \<open>nat\<setminus>{0}\<subseteq>nat\<close> have "H`(n #+ 1) \<in> H``(nat\<setminus>{0})"
    using func_imagedef by auto
  from assms(2,3,4) \<open>n\<in>nat\<close> have "H`(n #+ 1) O H`(n #+ 1) \<subseteq> H`(n)"
    using halving_seq_halves(2) by simp
  with \<open>B=H`(n)\<close> \<open>H`(n #+ 1) \<in> H``(nat\<setminus>{0})\<close> 
  show "\<exists>B\<^sub>2\<in>H``(nat\<setminus>{0}). B\<^sub>2 O B\<^sub>2 \<subseteq> B" by auto
qed

text\<open>The halving sequence image of positive naturals is contained 
  in the powerset of $X\times X$ and is not empty.\<close>

lemma halving_seq_base_5_and_6th_cond: 
  assumes "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)" 
  shows "H``(nat\<setminus>{0}) \<subseteq> Pow(X\<times>X)" and "H``(nat\<setminus>{0}) \<noteq> \<emptyset>" 
proof -
  from assms(2,3,4) have "H:nat\<rightarrow>\<Phi>" using halving_seq_start(1) by simp 
  then have "H``(nat\<setminus>{0}) \<subseteq> \<Phi>" using func1_1_L6(2) by simp
  with assms(1) show "H``(nat\<setminus>{0}) \<subseteq> Pow(X\<times>X)"
    using unif_filter unfolding IsFilter_def by blast
  have "nat\<setminus>{0} \<subseteq> nat" by auto
  with \<open>H:nat\<rightarrow>\<Phi>\<close> show "H``(nat\<setminus>{0}) \<noteq> \<emptyset>" using func_imagedef by auto
qed

text\<open>If $\Phi$ is a uniformity, $h$ is a halving function for it and $U\in \Phi$ 
  then the image of the halving sequence defined inductively as $H_0=U, H_{n+1} = h(H(n))$ 
  on the positive naturals is a fundamental system of entourages.\<close>

theorem halving_seq_base: 
  assumes "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  defines "H \<equiv> InductiveSequence(U,h)"
  shows "(H``(nat\<setminus>{0})) {is a uniform base on} X"
  using assms halving_seq_base_1st_cond halving_seq_base_2_3_4_conds 
    halving_seq_base_5_and_6th_cond
  unfolding IsUniformityBaseOn_def by simp

text\<open>Theorem \<open>halving_seq_base\<close> shows that given a halving function $h$
  for a uniformity $\Phi$ each entourage $U\in\Phi$ defines a  uniformity that 
  has a countable base consisting of symmetric entourages and (as we show later) 
  contains that entourage.
  The next definition summarizes the construction of this coarser uniformity.\<close>

definition 
  "CountBaseUnif(X,h,U) \<equiv> Supersets(X\<times>X,InductiveSequence(U,h)``(nat\<setminus>{0}))"

text\<open>If $\Phi$ is a uniformity, $h$ is a halving function for it and $U\in\Phi$ 
  then \<open>CountBaseUnif(X,h,U)\<close> is a uniformity on $X$ that contains $U$ (as a member) and
  is contained in $\Phi$.\<close>

lemma unif_count_base_unif: 
  assumes "X\<noteq>\<emptyset>" "\<Phi> {is a uniformity on} X" "U\<in>\<Phi>" "h {is a halving function for} \<Phi>"
  shows "CountBaseUnif(X,h,U) {is a uniformity on} X" and 
    "U\<in>CountBaseUnif(X,h,U)" "CountBaseUnif(X,h,U) \<subseteq> \<Phi>"
proof -
  let ?H = "InductiveSequence(U,h)"
  let ?\<Psi>\<^sub>U = "CountBaseUnif(X,h,U)"
  from assms show "?\<Psi>\<^sub>U {is a uniformity on} X"
    using halving_seq_base uniformity_base_is_base
    unfolding CountBaseUnif_def by simp
  from assms(2,3) have "U\<subseteq>X\<times>X" using entourage_props(1) by simp
  from assms(2,3,4) have "?H:nat\<rightarrow>\<Phi>" using halving_seq_start(1) 
    by simp
  with assms(2) have "?H`(1) \<subseteq> X\<times>X" 
    using apply_funtype entourage_props(1) by simp
  from assms(2,3,4) have "?H`(1) \<subseteq> U"
    using halving_seq_decr(1) halving_seq_start(2)
    unfolding IsDecreasingSeq_def InclusionOn_def by force
  have "1\<in>nat\<setminus>{0}" "nat\<setminus>{0} \<subseteq> nat" by auto
  with \<open>?H:nat\<rightarrow>\<Phi>\<close> have "?H`(1) \<in> ?H``(nat\<setminus>{0})" using func_imagedef 
    by auto
  with \<open>?H`(1) \<subseteq> X\<times>X\<close> have "?H`(1) \<in> ?\<Psi>\<^sub>U" 
    using superset_gen unfolding CountBaseUnif_def by simp
  with \<open>?\<Psi>\<^sub>U {is a uniformity on} X\<close>  \<open>U\<subseteq>X\<times>X\<close> \<open>?H`(1) \<subseteq> U\<close>  
  show "U\<in>?\<Psi>\<^sub>U" using unif_filter unfolding IsFilter_def by simp
  { fix V assume "V\<in>?\<Psi>\<^sub>U"
    then obtain W where "W\<subseteq>X\<times>X" "W \<in> ?H``(nat\<setminus>{0})" and "W\<subseteq>V"
      unfolding CountBaseUnif_def Supersets_def by auto
    from \<open>?\<Psi>\<^sub>U {is a uniformity on} X\<close> \<open>V\<in>?\<Psi>\<^sub>U\<close> have "V\<subseteq>X\<times>X"
      using entourage_props(1) by simp
    from \<open>?H:nat\<rightarrow>\<Phi>\<close> \<open>W\<in>?H``(nat\<setminus>{0})\<close> have "W\<in>\<Phi>"
      using func1_1_L6(2) by blast
    with assms(2) \<open>V\<subseteq>X\<times>X\<close> \<open>W\<subseteq>V\<close> have "V\<in>\<Phi>"
      using unif_filter unfolding IsFilter_def by simp
  } thus "?\<Psi>\<^sub>U \<subseteq> \<Phi>" by auto
qed

text\<open>If $\Phi$ is a uniformity on a nonempty set $X$ and admits a halving function $h$
  then $\Phi$ is the supremum of the collection of uniformities $\{\Psi_U: U\in\Phi\}$
  in the inclusion order relation, where $\Psi_U = $\<open>CountBaseUnif(X,h,U)\<close>.
  Since $\Psi_U$ has a enumerable base this shows that every uniformity 
  (that admits a halving function) is a union and a supremum of some collection of uniformities 
  each of which has an enumerable base. We are using the word "enumerable" in the sense
  defined in \<open>Countable vs enumerable\<close> section of the \<open>Cardinal_ZF\<close>, 
  note in ZF this is not the same as "countable".\<close>

theorem sup_count_base_unifs: 
  assumes "X\<noteq>\<emptyset>" "\<Phi> {is a uniformity on} X" "h {is a halving function for} \<Phi>"
  shows 
    "\<Phi> = \<Union>{CountBaseUnif(X,h,U). U\<in>\<Phi>}"
    "\<Phi> = Supremum(OrderOnUniformities(X),{CountBaseUnif(X,h,U). U\<in>\<Phi>})"
proof - 
  let ?\<U> = "{CountBaseUnif(X,h,U). U\<in>\<Phi>}"
  show "\<Phi> = \<Union>?\<U>"
  proof
    from assms show "\<Union>?\<U> \<subseteq> \<Phi>"
      using unif_count_base_unif(3) by blast
    from assms show "\<Phi> \<subseteq> \<Union>?\<U>"
      using unif_count_base_unif(2) by auto
  qed
  from assms have "?\<U>\<noteq>\<emptyset>" and "?\<U> \<subseteq> Uniformities(X)"
    using uniformity_non_empty unif_count_base_unif(1) unif_in_unifs 
    by auto
  with assms(1,2) \<open>\<Phi>=\<Union>?\<U>\<close> show "\<Phi> = Supremum(OrderOnUniformities(X),?\<U>)"
    using union_unif_sup by simp
qed

text\<open> The next goal we want to achieve is to show that if a uniformity
  has an enumerable base consisting of symmetric entourages $\{ V_n\}_{n\in\mathbb{N}}$
  then it can be represented by a pseudometric. In the Bourbaki's proof
  the first step is to inductively define a sequence $\{ V_n\}_{n\in\mathbb{N}}$ 
  of entourages with the property that $U_1 \subseteq V_1$ and for all natural
  $n\geq 1$ we have $U_{n+1}\circ U_{n+1}\circ U_{n+1} \subseteq U_n\cap V_n$.
  The existence of such sequence for every uniformity with countable base
  follows from theorem \<open>ustex3sym\<close> in \<open>UniformSpace_ZF\<close> theory and the Axiom of Choice.
  Here we construct this sequence from an analogue of the halving function but 
  with the property the $t(U)\circ t(u)\circ t(U)\subseteq U$ using the notion of 
  inductive sequence with changing generation function.
  The \<open>IsDiv3Function(\<Phi>,t)\<close> predicate defines an analogue of the halving function but 
  with the property the $t(U)\circ t(u)\circ t(U)\subseteq U$ for all $U\in \Phi$. \<close>

definition "IsDiv3Function(\<Phi>,t) \<equiv> t:\<Phi>\<rightarrow>\<Phi> \<and> 
  (\<forall>U\<in>\<Phi>. t`(U) = converse(t`(U)) \<and> t`(U) O (t`(U) O t`(U)) \<subseteq> U)" 



end